plugins {
	id 'java'
	id 'idea'
	id 'application'
}

final String resodayMainClassName = 'dev.andrybak.resoday.Resoday'
application {
	mainClass.set(resodayMainClassName)
	applicationName = 'resoday'
}

tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

jar {
	manifest {
		attributes(
			'Main-Class': resodayMainClassName
		)
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'dev.dirs:directories:26' // https://github.com/dirs-dev/directories-jvm
	implementation 'com.google.code.gson:gson:2.8.9'

	testImplementation 'org.junit.jupiter:junit-jupiter:5.8.1'
	testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
	useJUnitPlatform()

	testLogging { // add stdout logging for running `./gradlew test`
		lifecycle {
			events "passed", "skipped", "failed"
			exceptionFormat 'full'

			afterSuite { description, result ->
				if (!description.parent) { // will match the outermost suite
					def stats = "${result.testCount} tests run, ${result.successfulTestCount} successes, " +
						"${result.failedTestCount} failures, ${result.skippedTestCount} ignored"
					println('-' * stats.length())
					println("Testing result for ${project.name}: ${result.resultType}")
					println(stats)
					println('-' * stats.length())
				}
			}
		}
	}

	reports {
		junitXml.enabled = true
		html.enabled = true // see ./build/reports/tests/test/index.html
	}
}
